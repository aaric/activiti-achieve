plugins {
    id "java"
    id "io.freefair.lombok" version "8.3" apply false
    id "io.spring.dependency-management" version "1.1.3" apply false
    id "org.springframework.boot" version "3.0.2" apply false
//    id "org.springframework.boot" version "2.3.12.RELEASE" apply false
}

allprojects {
    group = "com.at7"
    version = "0.4.0-SNAPSHOT"

    ext {
        // https://github.com/alibaba/spring-cloud-alibaba/wiki/版本说明#毕业版本依赖关系推荐使用
        springCloudVersion = "2022.0.0"
        springCloudAlibabaVersion = "2022.0.0.0"
//        springCloudVersion = "Hoxton.SR12"
//        springCloudAlibabaVersion = "2.2.9.RELEASE"
        knife4jVersion = "4.3.0"
        activitiVersion = "7.4.0"
    }

    repositories {
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://repository.incarcloud.com/content/groups/public" }
        maven { url "https://artifacts.alfresco.com/nexus/content/repositories/activiti-releases" }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "checkstyle"
    apply plugin: "io.freefair.lombok"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    dependencies {
        implementation "org.slf4j:slf4j-api"

        testImplementation("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:$springCloudAlibabaVersion"
            mavenBom "com.github.xiaoymin:knife4j-dependencies:$knife4jVersion"
            mavenBom "org.activiti:activiti-dependencies:$activitiVersion"
//            mavenBom "org.activiti.cloud:activiti-cloud-dependencies:$activitiVersion"
        }

        dependencies {
            dependencySet(group: "org.slf4j", version: "2.0.9") {
                entry "slf4j-api"
                entry "slf4j-simple"
                entry "slf4j-log4j12"
            }

            dependency "org.apache.commons:commons-io:1.3.2"
            dependency "org.apache.commons:commons-lang3:3.13.0"
            dependency "org.apache.commons:commons-collections4:4.4"

            dependency "com.baomidou:mybatis-plus-boot-starter:3.5.4.1"
            dependency "com.alibaba:druid-spring-boot-starter:1.2.20"
            dependency "mysql:mysql-connector-java:8.0.33"
        }
    }

    test {
        useJUnitPlatform()
    }

    task checkstyle(type: Checkstyle) {
        configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures true
        showViolations true
        source = fileTree("src")
        include "**/*.java"
        exclude "**/test/**"
        exclude "**/build/**"
        classpath = files()
    }

    task removeLombokConfig() {
        def subProjectPath = project.projectDir
        if (null != subProjectPath) {
            def lombokConfigFile = new File(subProjectPath, "lombok.config")
            if (lombokConfigFile.exists()) {
                lombokConfigFile.delete()
            }
        }
    }

    task removeOutputLog() {
        def subProjectPath = project.projectDir
        if (null != subProjectPath) {
            subProjectPath.list().each { filename ->
                if (filename.endsWith(".log") || filename.equalsIgnoreCase("logs")) {
                    def subPath = new File(subProjectPath, filename)
                    if (subPath.isDirectory()) {
                        for (File leafFile : subPath.listFiles()) {
                            leafFile.delete()
                        }
                    }
                    subPath.delete()
                }
            }
        }
    }

    defaultTasks "assemble"
}
